#Copyright (c) 2020-2021 Huawei Device Co., Ltd.
#Licensed under the Apache License, Version 2.0 (the "License");
#you may not use this file except in compliance with the License.
#You may obtain a copy of the License at
#
#    http://www.apache.org/licenses/LICENSE-2.0
#
#Unless required by applicable law or agreed to in writing, software
#distributed under the License is distributed on an "AS IS" BASIS,
#WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#See the License for the specific language governing permissions and
#limitations under the License.

import("//build/lite/config/component/lite_component.gni")
import("//build/lite/config/subsystem/aafwk/config.gni")
import("//foundation/ace/ace_engine_lite/ace_lite.gni")

declare_args() {
  LOSCFG_TEST_JS_BUILD = false
}

lite_component("jsfwk") {
  features = [ ":ace_lite" ]
}

config("ace_lite_config") {
  include_dirs = ace_lite_include_dirs
  if (ohos_kernel_type == "liteos_a" || ohos_kernel_type == "liteos_m") {
    include_dirs += [ "targets/liteos_a" ]
  } else if (ohos_kernel_type == "linux") {
    include_dirs += [ "targets/linux" ]
  }
  cflags_cc = [ "-std=c++14" ]
  ldflags = [ "-lstdc++" ]
  ldflags += [ "-lpthread" ]
  ldflags += [ "-Wl,-rpath-link=$ohos_root_path/$root_out_dir" ]
}

shared_library("ace_lite") {
  configs -= [ "//build/lite/config:language_cpp" ]
  cflags = [
    "-Wall",
    "-Wno-format",
    "-Wno-format-extra-args",
  ]
  cflags_cc = cflags
  configs += [ ":ace_lite_config" ]
  public_configs = configs

  sources = ace_lite_sources

  public_deps = [
    "${appexecfwk_lite_path}/interfaces/kits/bundle_lite/js/builtin:capability_api",
    "//base/global/i18n_lite/frameworks/i18n:global_i18n",
    "//base/global/resmgr_lite/frameworks/resmgr_lite:global_resmgr",
    "//base/security/huks/frameworks/crypto_lite/js/builtin:ace_kit_cipher",
    "//build/lite/config/component/cJSON:cjson_shared",
    "//foundation/graphic/surface:lite_surface",
    "//foundation/graphic/ui:lite_ui",
    "//foundation/multimedia/camera_lite/frameworks:camera_lite",
    "//foundation/multimedia/media_lite/frameworks/player_lite:player_lite",
    "//foundation/multimedia/media_lite/interfaces/kits/player_lite/js/builtin:audio_lite_api",
    "//third_party/bounds_checking_function:libsec_shared",
    "//third_party/jerryscript/jerry-core:jerry-core_shared",
    "//third_party/jerryscript/jerry-ext:jerry-ext_shared",
    "//third_party/jerryscript/jerry-libm:jerry-libm_shared",
    "//third_party/jerryscript/jerry-port/default:jerry-port-default_shared",
    "//utils/native/lite/js/builtin:ace_utils_kits",
    "//utils/native/lite/timer_task:ace_kit_timer",
  ]

  defines = []

  if (ohos_build_type == "debug") {
    defines += [ "JS_PROFILER=1" ]
  } else {
    defines += [ "JS_PROFILER=0" ]
  }

  if (LOSCFG_TEST_JS_BUILD) {
    defines += [ "JSFWK_TEST" ]
  }

  if (enable_ohos_appexecfwk_feature_ability == true) {
    defines += [ "ABILITY_WINDOW_SUPPORT" ]
  }
}
